
array palette, charset, listitems, background;

array tg_o, tg_t, tg_p;
array tg_x, tg_y, tg_w, tg_h, tg_m, tg_k, tg_a, tg_b, tg_c;

global tg_selx, tg_sely, tg_focus;

function tg_charset( str ) {
	var w, h, clr, chr, y, x;
	var stride, stroff, chroff;
	let stride = 9;
	if <( $chr( str 9 ) 32 ) {
		let stride = 10;
	}
	let w = *( 8 96 );
	let h = *( 16 $len( palette ) );
	dim [ charset 0 ];
	dim [ charset *( w h ) ];
	let clr = 0;
	while <( clr $len( palette ) ) {
		let stroff = 0;
		let chr = 1;
		while <( chr 95 ) {
			let chroff = +( *( clr *( w 16 ) ) *( chr 8 ) );
			let y = 0;
			while <( y 16 ) {
				let x = 0;
				while <( x 8 ) {
					if >( $chr( str +( stroff x ) ) 32 ) {
						set [ charset +( chroff x ) ] = +( *( [ palette clr ] 256 ) 255 );
					}
					inc x;
				}
				let stroff = +( stroff stride );
				let chroff = +( chroff w );
				inc y;
			}
			inc chr;
		}
		inc clr;
	}
	fxsurface 0, w, h, charset;
}

function tg_text( text, x, y, clr ) {
	var idx, len;
	let len = $len( text );
	while <( idx len ) {
		fxblit 0, *( -( $chr( text idx ) 32 ) 8 ), *( clr 16 ), 8, 16, +( x *( idx 8 ) ), y;
		inc idx;
	}
}

function tg_box( x, y, w, h, c1, c2 ) {
	fxrect x, y, 2, h, c1;
	fxrect +( x -( w 2 ) ), y, 2, h, c2;
	fxrect x, y, -( w 1 ), 2, c1;
	fxrect +( x 1 ), +( y -( h 2 ) ), -( w 1 ), 2, c2;
}

function tg_raise_box( x, y, w, h ) {
	call tg_box( x, y, w, h, [ palette 1 ], [ palette 0 ] );
}

function tg_lower_box( x, y, w, h ) {
	call tg_box( x, y, w, h, [ palette 0 ], [ palette 1 ] );
}

function tg_bevel_box( x, y, w, h ) {
	call tg_box( x, y, w, h, [ palette 1 ], [ palette 0 ] );
	call tg_box( +( x 2 ), +( y 2 ), -( w 4 ), -( h 4 ), [ palette 0 ], [ palette 1 ] );
}

function tg_resize( n, x, y, w, h ) {
	var g, k, a;
	if <( w [ tg_m n ] ) {
		let w = [ tg_m n ];
	}
	if <( h [ tg_m n ] ) {
		let h = [ tg_m n ];
	}
	if =( [ tg_t n ] 1 ) {
		while <( g $len( tg_t ) ) {
			if =( [ tg_p g ] n ) {
				let k = [ tg_k g ];
				if &( k 4 ) {
					if &( k 1 ) {
						let a = +( [ tg_h g ] -( h [ tg_h n ] ) );
						if <( a [ tg_m g ] ) {
							let a = [ tg_m g ];
						}
						set [ tg_h g ] = a;
					} else {
						set [ tg_y g ] = +( [ tg_y g ] -( h [ tg_h n ] ) );
					}
				}
				if &( k 8 ) {
					if &( k 2 ) {
						let a = +( [ tg_w g ] -( w [ tg_w n ] ) );
						if <( a [ tg_m g ] ) {
							let a = [ tg_m g ];
						}
						set [ tg_w g ] = a;
					} else {
						set [ tg_x g ] = +( [ tg_x g ] -( w [ tg_w n ] ) );
					}
				}
			}
			inc g;
		}
	}
	set [ tg_x n ] = x;
	set [ tg_y n ] = y;
	set [ tg_w n ] = w;
	set [ tg_h n ] = h;
}

function tg_minsize( n, size ) {
	set [ tg_m n ] = size;
}

function tg_anchor( n, top, left, bottom, right ) {
	var a;
	if top { let a = |( a 1 ); }
	if left { let a = |( a 2 ); }
	if bottom { let a = |( a 4 ); }
	if right { let a = |( a 8 ); }
	set [ tg_k n ] = a;
}

function tg_window( n, parent, x, y, w, h, text ) {
	set [ tg_t n ] = 1;
	set [ tg_p n ] = parent;
	set [ tg_m n ] = 16;
	call tg_resize( n, x, y, w, h );
	set [ tg_a n ] = text;
}

function tg_button( n, parent, x, y, w, h, text ) {
	call tg_window( n, parent, x, y, w, h, text );
	set [ tg_t n ] = 2;
}

function tg_vslide( n, parent, x, y, w, h, page, count ) {
	call tg_window( n, parent, x, y, w, h, 0 );
	set [ tg_t n ] = 3;
	set [ tg_b n ] = page;
	set [ tg_c n ] = count;
}

function tg_textbox( n, parent, x, y, w, h, text ) {
	call tg_window( n, parent, x, y, w, h, text );
	set [ tg_t n ] = 4;
}

function tg_sizegad( n, parent, x, y, w, h ) {
	call tg_window( n, parent, x, y, w, h, 0 );
	call tg_anchor( n, 0, 0, 1, 1 );
	set [ tg_t n ] = 5;
}

function tg_listbox( n, parent, x, y, w, h, list ) {
	call tg_window( n, parent, x, y, w, h, list );
	set [ tg_t n ] = 6;
}

function tg_init( gadcount ) {
	var idx;
	dim [ tg_o gadcount ];
	dim [ tg_t gadcount ];
	dim [ tg_p gadcount ];
	dim [ tg_x gadcount ];
	dim [ tg_y gadcount ];
	dim [ tg_w gadcount ];
	dim [ tg_h gadcount ];
	dim [ tg_m gadcount ];
	dim [ tg_k gadcount ];
	dim [ tg_a gadcount ];
	dim [ tg_b gadcount ];
	dim [ tg_c gadcount ];
	while <( idx gadcount ) {
		set [ tg_o idx ] = idx;
		inc idx;
	}
}

function tg_draw_background() {
	var y, h;
	let h = $len( background );
	while <( y h ) {
		fxrect 0, *( y 2 ), 640, 2, [ background y ];
		inc y;
	}
	fxblit 0, 0, 0, 640, 480, 0, 128;
}

function tg_getx( n ) {
	var x;
	let x = [ tg_x n ];
	if >( n 0 ) {
		let x = +( x tg_getx( [ tg_p n ] ) );
	}
	return x;
}

function tg_gety( n ) {
	var y;
	let y = [ tg_y n ];
	if >( n 0 ) {
		let y = +( y tg_gety( [ tg_p n ] ) );
	}
	return y;
}

function tg_draw_window( n ) {
	var x, y, w, h;
	let x = tg_getx( n );
	let y = tg_gety( n );
	let w = [ tg_w n ];
	let h = [ tg_h n ];
	fxrect x, y, w, 26, [ palette 2 ];
	fxrect x, +( y 26 ), w, -( h 26 ), [ palette 3 ];
	call tg_raise_box( x, y, w, h );
	call tg_lower_box( +( x 2 ), +( y 24 ), -( w 4 ), -( h 26 ) );
	call tg_text( [ tg_a n ], +( x 6 ), +( y 6 ), 0 );
}

function tg_vslide_size( n ) {
	return +( /( *( -( [ tg_h n ] 12 ) [ tg_b n ] ) [ tg_c n ] ) 8 );
}

function tg_vslide_offset( n ) {
	return /( *( -( [ tg_h n ] 12 ) [ tg_a n ] ) [ tg_c n ] );
}

function tg_draw_vslide( n ) {
	var x, y, w, h, s, d;
	let x = tg_getx( n );
	let y = tg_gety( n );
	let w = [ tg_w n ];
	let h = [ tg_h n ];
	let s = tg_vslide_size( n );
	let d = tg_vslide_offset( n );
	fxrect x, y, w, h, [ palette 3 ];
	call tg_lower_box( x, y, w, h );
	fxrect +( x, 2 ), +( +( y d ) 2 ), -( w 4 ), s, [ palette 2 ];
	call tg_raise_box( +( x, 2 ), +( +( y d ) 2 ), -( w 4 ), s );
}

function tg_draw_button( n ) {
	var x, y, bg, hl, sh;
	let x = tg_getx( n );
	let y = tg_gety( n );
	if =( [ tg_b n ] 1 ) {
		let bg = 3;
		let hl = 0;
		let sh = 1;
	} else {
		let bg = 2;
		let hl = 1;
		let sh = 0;
	}
	fxrect x, y, [ tg_w n ], [ tg_h n ], [ palette bg ];
	call tg_box( x, y, [ tg_w n ], [ tg_h n ],
		[ palette hl ], [ palette sh ] );
	call tg_text( [ tg_a n ],
		+( x /( -( [ tg_w n ] *( $len( [ tg_a n ] ) 8 ) ) 2 ) ),
		+( y /( -( [ tg_h n ] 14 ) 2 ) ), sh );
}

function tg_draw_textbox( n ) {
	var x, y, t, c;
	let x = tg_getx( n );
	let y = tg_gety( n );
	let t = [ tg_a n ];
	let c = /( -( [ tg_w n ] 16 ) 8 );
	if >( $len( t ) c ) {
		let t = $sub( t, -( $len( t ) c ), c );
	}
	fxrect x, y, [ tg_w n ], [ tg_h n ], [ palette 2 ];
	if =( [ tg_b n ] 1 ) {
		fxrect +( x *( +( $len( t ) 1 ) 8 ) ), +( y 6 ), 8, 16, [ palette 3 ];
	}
	call tg_box( x, y, [ tg_w n ], [ tg_h n ], [ palette 1 ], [ palette 0 ] );
	call tg_box( +( x 2 ), +( y 2 ), -( [ tg_w n ] 4 ), -( [ tg_h n ] 4 ),
		[ palette 0 ], [ palette 1 ] );
	call tg_text( t, +( x 8 ), +( y 6 ), 0 );
}

function tg_draw_sizegad( n ) {
	var x, y;
	let x = tg_getx( n );
	let y = tg_gety( n );
	fxrect x, y, [ tg_w n ], [ tg_h n ], [ palette 2 ];
	call tg_box( x, y, [ tg_w n ], [ tg_h n ], [ palette 1 ], [ palette 0 ] );
}

function tg_draw_listbox( n ) {
	var x, y, tw, th, idx, end, ty, tc, txt;
	let x = tg_getx( n );
	let y = tg_gety( n );
	fxrect x, y, [ tg_w n ], [ tg_h n ], [ palette 2 ];
	call tg_box( x, y, [ tg_w n ], [ tg_h n ], [ palette 0 ], [ palette 1 ] );
	let tw = /( -( [ tg_w n ] 16 ) 8 );
	let th = /( -( [ tg_h n ] 12 ) 16 );
	if >( +( [ tg_b n ] th ) $len( [ tg_a n ] ) ) {
		let th = -( $len( [ tg_a n ] ) [ tg_b n ] );
	}
	let idx = [ tg_b n ];
	let end = +( idx th );
	while <( idx end ) {
		let ty = +( +( y *( idx 16 ) ) 6 );
		let tc = 0;
		if =( [ tg_c n ] idx ) {
			fxrect +( x 8 ), ty, -( [ tg_w n ] 16 ), 16, [ palette 3 ];
			let tc = 1;
		}
		let txt = [ [ tg_a n ] idx ];
		if >( $len( txt ) tw ) {
			let txt = $sub( txt, 0, tw );
		}
		call tg_text( txt, +( x 8 ), ty, tc );
		inc idx;
	}
}

function tg_draw( n ) {
	var idx, len;
	if <( n 1 ) {
		call tg_draw_background();
	} else {
		switch [ tg_t n ] {
			case 1 { call tg_draw_window( n ); }
			case 2 { call tg_draw_button( n ); }
			case 3 { call tg_draw_vslide( n ); }
			case 4 { call tg_draw_textbox( n ); }
			case 5 { call tg_draw_sizegad( n ); }
			case 6 { call tg_draw_listbox( n ); }
		}
	}
	let idx = 1;
	let len = $len( tg_o );
	while <( idx len ) {
		if =( [ tg_p [ tg_o idx ] ] n ) {
			call tg_draw( [ tg_o idx ] );
		}
		inc idx;
	}
}

function tg_find( x, y, p ) {
	var n, idx, len, g, x0, y0, x1, y1;
	let n = p;
	let idx = 1;
	let len = $len( tg_o );
	while <( idx len ) {
		let g = [ tg_o idx ];
		if =( [ tg_p g ] p ) {
			let x0 = [ tg_x g ];
			let y0 = [ tg_y g ];
			let x1 = +( x0 [ tg_w g ] );
			let y1 = +( y0 [ tg_h g ] );
			if &&( &&( >e( x x0 ) >e( y y0 ) ) &&( <( x x1 ) <( y y1 ) ) ) {
				let n = tg_find( -( x x0 ), -( y y0 ), g );
			}
		}
		inc idx;
	}
	return n;
}

function tg_wintofront( n ) {
	var idx, end, g;
	while >( [ tg_t n ] 1 ) {
		let n = [ tg_p n ];
	}
	let end = -( $len( tg_o ) 1 );
	if &&( >( n 0 ) !( =( [ tg_o end ] n ) ) ) {
		let idx = 1;
		while <( idx $len( tg_o ) ) {
			let g = [ tg_o -( idx 1 ) ];
			if =( g n ) {
				set [ tg_o -( idx 1 ) ] = [ tg_o idx ];
				set [ tg_o idx ] = g;
			}
			inc idx;
		}
		call tg_draw( [ tg_o end ] );
	}
	return n;
}

function tg_scan( event ) {
	var n, s, o, p, e, t, c, w, x, y, selected;
	switch event {
		case FX_MOUSEMOTION {
			if =( $mousekey 1 ) {
				if =( tg_selx 0 ) {
					let tg_selx = $xmouse;
					let tg_sely = $ymouse;
				}
				let n = tg_find( tg_selx, tg_sely, 0 );
				let w = tg_wintofront( n );
				switch [ tg_t n ] {
					case 1 {
						let x = $xmouse;
						let y = $ymouse;
						set [ tg_x n ] = -( +( [ tg_x n ] x ) tg_selx );
						set [ tg_y n ] = -( +( [ tg_y n ] y ) tg_sely );
						let tg_selx = x;
						let tg_sely = y;
						call tg_draw( 0 );
					}
					case 2 {
						if =( tg_find( $xmouse, $ymouse, 0 ) n ) {
							set [ tg_b n ] = 1;
						} else {
							set [ tg_b n ] = 0;
						}
						call tg_draw( n );
					}
					case 3 {
						let s = tg_vslide_size( n );
						let o = tg_vslide_offset( n );
						let e = -( -( [ tg_h n ] 4 ) s );
						let y = +( +( tg_gety( n ) o ) 2 );
						if &&( >( tg_sely y ) <( tg_sely +( y s ) ) ) {
							let p = +( o -( $ymouse tg_sely ) );
							if <( p 0 ) {
								let p = 0;
							}
							if >( p e ) {
								let p = e;
							}
							if >( p 0 ) {
								set [ tg_a n ] = /( *( p -( [ tg_c n ] [ tg_b n ] ) ) e );
							} else {
								set [ tg_a n ] = 0;
							}
							let tg_sely = +( tg_sely -( tg_vslide_offset( n ) o ) );
							let selected = n;
							call tg_draw( n );
						}
					}
					case 5 {
						let x = $xmouse;
						let y = $ymouse;
						let o = -( tg_selx tg_getx( n ) );
						let p = -( tg_sely tg_gety( n ) );
						call tg_resize( w, [ tg_x w ], [ tg_y w ], 
							-( +( [ tg_w w ] x ) tg_selx ),
							-( +( [ tg_h w ] y ) tg_sely ) );
						let tg_selx = +( tg_getx( n ) o );
						let tg_sely = +( tg_gety( n ) p );
						call tg_draw( 0 );
					}
				}
			}
		}
		case FX_MOUSEKEYDOWN {
			let tg_selx = $xmouse;
			let tg_sely = $ymouse;
			let n = tg_find( tg_selx, tg_sely, 0 );
			if =( [ tg_t tg_focus ] 4 ) {
				set [ tg_b tg_focus ] = 0;
				call tg_draw( tg_focus );
			}
			let tg_focus = n;
			let w = tg_wintofront( n );
			switch [ tg_t n ] {
				case 2 {
					set [ tg_b n ] = 1;
					call tg_draw( n );
				}
				case 3 {
					let s = tg_vslide_size( n );
					let o = tg_vslide_offset( n );
					let e = -( -( [ tg_h n ] 4 ) s );
					let y = +( +( tg_gety( n ) o ) 2 );
					if <( tg_sely y ) {
						let t = -( [ tg_a n ] [ tg_b n ] );
						if <( t 0 ) {
							let t = 0;
						}
						set [ tg_a n ] = t;
						let selected = n;
						call tg_draw( n );
					}
					if >( tg_sely +( y s ) ) {
						let t = +( [ tg_a n ] [ tg_b n ] );
						if >( t -( [ tg_c n ] [ tg_b n ] ) ) {
							let t = -( [ tg_c n ] [ tg_b n ] );
						}
						set [ tg_a n ] = t;
						let selected = n;
						call tg_draw( n );
					}
				}
				case 4 {
					set [ tg_b n ] = 1;
					call tg_draw( n );
				}
				case 6 {
					let s = +( [ tg_b n ] /( -( tg_sely +( tg_gety( n ) 6 ) ) 16 ) );
					if <( s $len( [ tg_a n ] ) ) {
						set [ tg_c n ] = s;
					}
					let selected = n;
					call tg_draw( n );
				}
			}
		}
		case FX_MOUSEKEYUP {
			let n = tg_find( tg_selx, tg_sely, 0 );
			if =( [ tg_t n ] 2 ) {
				set [ tg_b n ] = 0;
				call tg_draw( n );
				if =( tg_find( $xmouse, $ymouse, 0 ) n ) {
					let selected = n;
				}
			}
			let tg_selx = 0;
			let tg_sely = 0;
		}
		case FX_KEYDOWN {
			if =( [ tg_t tg_focus ] 4 ) {
				let t = [ tg_a tg_focus ];
				let c = $keyboard;
				if =( c 8 ) {
					if >( $len( t ) 0 ) {
						if &( $keyshift 3 ) {
							let t = "";
						} else {
							let t = $sub( t, 0, -( $len( t ) 1 ) );
						}
						set [ tg_a tg_focus ] = t;
					}
				} else {
					if >( c 31 ) {
						if &( $keyshift 3 ) {
							if <( c 64 ) {
								let c = $chr( " !\"~$%&@()*+<_>?)!\"#$%^&*(::<+>?@", -( c 32 ) );
							} else {
								if <( c 96 ) {
									let c = +( c 32 );
								} else {
									let c = -( c 32 );
								}
							}
						}
						if <( c 127 ) {
							set [ tg_a tg_focus ] = $str( t, $asc( c ) );
						}
					}
				}
				let selected = tg_focus;
				call tg_draw( tg_focus );
			}
		}
	}
	return selected;
}

function gradient( c1, c2, out, off, len ) {
	var r1, r2, g1, g2, b1, b2, r, g, b, idx;
	let r1 = &( >>( c1 16 ) 0xFF );
	let r2 = &( >>( c2 16 ) 0xFF );
	let g1 = &( >>( c1 8 ) 0xFF );
	let g2 = &( >>( c2 8 ) 0xFF );
	let b1 = &( c1 0xFF );
	let b2 = &( c2 0xFF );
	while <( idx len ) {
		let r = +( r1 /( *( -( r2 r1 ) idx ) len ) );
		let g = +( g1 /( *( -( g2 g1 ) idx ) len ) );
		let b = +( b1 /( *( -( b2 b1 ) idx ) len ) );
		set [ out +( off idx ) ] = +( *( r 65536 ) +( *( g 256 ) b ) );
		inc idx;
	}
}

program tgads {
	var selected;
	let palette = ${ 0x000000, 0xFFFFFF, 0xA0A0A0, 0x6080B0 };

	dim [ background 240 ];
	call gradient( 0x008000, 0x000080, background, 0, 240 );

	call tg_charset( $load( "topaz8.txt" ) );

	fxopen 640, 480, "tgads";

	call tg_init( 20 );

	call tg_button( 1, 0, 10, 32, 80, 26, "Button 1" );

	call tg_window( 2, 0, 10, 10, 200, 100, "Window 2" );
	call tg_button( 3, 2, 10, 32, 80, 26, "Button 3" );
	call tg_button( 4, 2, 10, 64, 80, 26, "Button 4" );

	call tg_window( 5, 0, 40, 40, 200, 200, "Window 5" );
	call tg_minsize( 5, 64 );
	call tg_sizegad( 6, 5, 180, 180, 16, 16 );

	call tg_window( 7, 0, 30, 30, 400, 200, "Window 6" );
	call tg_minsize( 7, 150 );
	call tg_button( 8, 7, 10, 32, 80, 26, "Button 7" );
	call tg_vslide( 9, 7, 370, 32, 20, 120, 5, 15 );
	call tg_anchor( 9, 1, 0, 1, 1 );
	call tg_textbox( 10, 7, 10, 64, 80, 28, "Text 9" );
	call tg_textbox( 11, 7, 100, 160, 260, 28, "Text 10" );
	call tg_anchor( 11, 0, 1, 1, 1 );
	call tg_sizegad( 12, 7, 380, 180, 16, 16 );

	let listitems = $fxdir( "." );

	call tg_listbox( 13, 7, 100, 32, 260, 120, listitems );
	call tg_anchor( 13, 1, 1, 1, 1 );
	call tg_draw( 0 );

	fxshow;

	while TRUE {
		let selected = tg_scan( $fxwait );
		if >( selected 0 ) {
			print $cat( selected, " ", [ tg_a selected ] );
		}
		fxshow;
	}
}
