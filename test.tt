
rem { Test }

const ONE = 1;
const STR = "*";
const ELE = ${ ( * ) };

global g;
array a;

include "hello.tt";

function assert( value, line ) {
	if !( value ) {
		throw $str( "Assertion failed on line " line );
	}
}

function testvoid() {
}

function testvars(x) {
	var y;
	let y = x;
	let g = y;
	call assert( =( g ONE ), $line( ${-} ) );
	call assert( =( TRUE 1 ), $line( ${-} ) );
	call assert( =( FALSE 0 ), $line( ${-} ) );
}

function testarray() {
	var err;
	dim [ a 3 ];
	set [ a 0 ] = "a";
	set [ a 1 ] = 1;
	set [ a 2 ] = [ a 0 ];
	dim [ a 2 ];
	try {
		set [ a 2 ] = 2;
		throw 0;
	} catch err {
	}
	call assert( =( err 2 ), $line( ${-} ) );
	dim [ a 1 ];
	call assert( =( $cmp( [ a 0 ] "a" ) 0 ), $line( ${-} ) );
	let a = ${ 0, $str( "\"one" "\"" ), $tup( "\\two\\"2 ), "\063", ${ four(4) }, 84, 84 };
	let a = $parse( $arr( a ) );
	call assert( =( $len( a ) 7 ), $line( ${-} ) );
	call assert( =( [ a 0 ] 0 ), $line( ${-} ) );
	call assert( =( $cmp( [ a 1 ] "\"one\"" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( [ a 2 ] "\\two\\" ) 0 ), $line( ${-} ) );
	call assert( =( [ a 2 ] 2 ), $line( ${-} ) );
	call assert( =( $cmp( [ a 3 ] "3" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( [ a 4 ] "four" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $child( $next( [ a 4 ] ) ) "4" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $sub( a, 5, 2 ) "TT" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( a "#Array#" ) 0 ), $line( ${-} ) );
}

function testfact() {
	if !('( fact( 3 ) = 6 ) ) {
		throw "testfact()";
	}
}

function fact( x ) {
	if >( x 1 ) {
		return *( x fact( -( x 1 ) ) );
	} else {
		return 1;
	}
}

function testswitch() {
	var x;
	switch $tup( "a" 1 ) {
		case 1   { let x = 0; }
		case "a" { let x = 0; }
		case $tup( "a" 1 ) { inc x; }
	}
	switch "a" {
		case "a" { inc x; }
	}
	switch 1 {
		case 1 { inc x; }
		default { let x = 0; }
	}
	switch 2 {
		default { inc x; }
	}
	call assert( =( x 4 ), $line( ${-} ) );
}

function testif() {
	var x;
	if FALSE { call assert( 0, $line( ${-} ) ); }
	if TRUE { let x = 2; }
	call assert( =( x 2 ), $line( ${-} ) );
	if "" { let x = 3; }
	call assert( =( x 3 ), $line( ${-} ) );
}

function testwhile() {
	var e, n;
	while <( n 1 ) { inc n; }
	call assert( =( n 1 ), $line( ${-} ) );
	while <( n 10 ) {
		break;
		call assert( FALSE, $line( ${-} ) );
	}
	while <( n 10 ) {
		if =( n 5 ) { break; }
		inc n;
		continue;
		call assert( FALSE, $line( ${-} ) );
	}
	call assert( =( n 5 ), $line( ${-} ) );
	while TRUE {
		try { break; } catch e {}
		call assert( FALSE, $line( ${-} ) );
	}
	while TRUE {
		try { throw 0; } catch e { break; }
		call assert( FALSE, $line( ${-} ) );
	}
	while TRUE {
		return 0;
		call assert( FALSE, $line( ${-} ) );
	}
}

function testtry() {
	var e;
	try {} catch e { call assert( FALSE, $line( ${-} ) ); }
	try {
		try {
			throw 1;
		} catch e {
			call assert( =( e 1 ), $line( ${-} ) );
			throw 2;
		}
	} catch e {
	}
	call assert( =( e 2 ), $line( ${-} ) );
	try { try { return 0; } catch e {} } catch e {}
	call assert( FALSE, $line( ${-} ) );
}

function testinfix() {
	call assert( '( ONE=1 ), $line( ${-} ) );
}

function testarith() {
	var e;
	call assert( =( +( +( 1 1 ) +( 1 1 ) ) 4 ), $line( ${-} ) );
	call assert( =( +( 1 1 ) 2 ), $line( ${-} ) );
	call assert( =( -( 1 1 ) 0 ), $line( ${-} ) );
	call assert( =( *( 2 2 ) 4 ), $line( ${-} ) );
	call assert( =( /( 6 2 ) 3 ), $line( ${-} ) );
	try { call /( 1 0 ); } catch e {}
	call assert( e, $line( ${-} ) );
	call assert( =( %( 6 4 ) 2 ), $line( ${-} ) );
	let e = 0;
	try { call %( 1 0 ); } catch e {}
	call assert( e, $line( ${-} ) );
	call assert( =( >>( -8 1 ) -4 ), $line( ${-} ) );
	call assert( =( 1 1 ), $line( ${-} ) );
	call assert( =( =( 1 0 ) 0 ), $line( ${-} ) );
	call assert( <( 0 1 ), $line( ${-} ) );
	call assert( =( <( 1 1 ) 0 ), $line( ${-} ) );
	call assert( <e( 0 1 ), $line( ${-} ) );
	call assert( <e( 1 1 ), $line( ${-} ) );
	call assert( =( <e( 1 0 ) 0 ), $line( ${-} ) );
	call assert( >( 1 0 ), $line( ${-} ) );
	call assert( =( >( 1 1 ) 0 ), $line( ${-} ) );
	call assert( >e( 1 0 ), $line( ${-} ) );
	call assert( >e( 1 1 ), $line( ${-} ) );
	call assert( =( >e( 0 1 ) 0 ), $line( ${-} ) );
	call assert( =( &( 1 2 ) 0 ), $line( ${-} ) );
	call assert( =( &( 2 2 ) 2 ), $line( ${-} ) );
	call assert( =( |( 1 2 ) 3 ), $line( ${-} ) );
	call assert( =( ^( 1 0 ) 1 ), $line( ${-} ) );
	call assert( =( ^( 1 1 ) 0 ), $line( ${-} ) );
	call assert( ~( 0 ), $line( ${-} ) );
}

function testlogic() {
	call assert( =( !( 0 ) 1 ), $line( ${-} ) );
	call assert( =( !( 1 ) 0 ), $line( ${-} ) );
	call assert( =( !( "" ) 0 ), $line( ${-} ) );
	call assert( =( &&( 0 0 ) 0 ), $line( ${-} ) );
	call assert( =( &&( 0 1 ) 0 ), $line( ${-} ) );
	call assert( =( &&( 1 0 ) 0 ), $line( ${-} ) );
	call assert( =( &&( 1 1 ) 1 ), $line( ${-} ) );
	call assert( =( &&( 1 "a" ) 1 ), $line( ${-} ) );
	call assert( =( &&( "a" 1 ) 1 ), $line( ${-} ) );
	call assert( =( &&( 0 /( 1 0 ) ) 0 ), $line( ${-} ) );
	call assert( =( ||( 0 0 ) 0 ), $line( ${-} ) );
	call assert( =( ||( 0 1 ) 1 ), $line( ${-} ) );
	call assert( =( ||( 1 0 ) 1 ), $line( ${-} ) );
	call assert( =( ||( 1 1 ) 1 ), $line( ${-} ) );
	call assert( =( ||( 0 "a" ) 1 ), $line( ${-} ) );
	call assert( =( ||( "a" 0 ) 1 ), $line( ${-} ) );
	call assert( =( ||( 1 /( 1 0 ) ) 1 ), $line( ${-} ) );
}

function testcmp() {
	var e;
	try { call $cmp( 0 "" ); } catch e {}
	call assert( e, $line( ${-} ) );
	let e = 0;
	try { call $chr( "" 0 ); } catch e {}
	call assert( =( $cmp( "0" "0" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( STR "*" ) 0 ), $line( ${-} ) );
	call assert( >( $cmp( "1" "0" ) 0 ), $line( ${-} ) );
	call assert( <( $cmp( "0" "1" ) 0 ), $line( ${-} ) );
	call assert( >( $cmp( "aa" "a" ) 0 ), $line( ${-} ) );
	call assert( <( $cmp( "a" "aa" ) 0 ), $line( ${-} ) );
	call assert( <( $cmp( "b" "ba" ) 0 ), $line( ${-} ) );
}

function testcat() {
	call assert( =( $cmp( $cat( 0 ) "0" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $cat( 0 1 ) "01" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $cat( "" 0 ) "0" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $cat( 0 "" ) "0" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $cat( 0 "1" "2" ) "012" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $cat( a ) a ) 0 ), $line( ${-} ) );
}

function testchr() {
	var e;
	try { call $chr( 0 0 ); } catch e {}
	call assert( e, $line( ${-} ) );
	let e = 0;
	try { call $chr( "" 0 ); } catch e {}
	call assert( e, $line( ${-} ) );
	call assert( =( $chr( "~" 0 ) 126 ), $line( ${-} ) );
}

function testsub() {
	var e;
	try { call $sub( 0 0 0 ); } catch e {}
	call assert( e, $line( ${-} ) );
	dim [ a 1 ];
	let e = 0;
	try { call $sub( a 1 1 ); } catch e {}
	call assert( e, $line( ${-} ) );
	let e = 0;
	try { call $sub( "a" 1 1 ); } catch e {}
	call assert( e, $line( ${-} ) );
	let e = 0;
	try { call $sub( "a" -1 1 ); } catch e {}
	call assert( e, $line( ${-} ) );
	let e = 0;
	try { call $sub( "a" 0 -1 ); } catch e {}
	call assert( e, $line( ${-} ) );
	call assert( =( $cmp( $sub( "abc" 1 1 ) "b" ) 0 ), $line( ${-} ) );
	let a = ${ 65, 66, 67 };
	call assert( =( $cmp( $sub( a 0 3 ) "ABC" ) 0 ), $line( ${-} ) );
}

function testasc() {
	call assert( =( $cmp( $asc( 126 ) "~" ) 0 ), $line( ${-} ) );
}

function testhex() {
	call assert( =( $cmp( $hex(  0x00000000 ) " 0x00000000" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $hex( -0x00000001 ) "-0x00000001" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $hex( -0x80000000 ) "-0x80000000" ) 0 ), $line( ${-} ) );
}

function testint() {
	var e;
	call assert( =( $int( "1" ) 1 ), $line( ${-} ) );
	call assert( =( $int( "0xA" ) 10 ), $line( ${-} ) );
	call assert( =( $int( "010" ) 8 ), $line( ${-} ) );
	try{ call $int( "a" ); } catch e {}
	call assert( e, $line( ${-} ) );
	let e = 0;
	try{ call $int( e ); } catch e {}
	call assert( e, $line( ${-} ) );
}

function testlen() {
	var e;
	call assert( =( $len( "" ) 0 ), $line( ${-} ) );
	call assert( =( $len( "a" ) 1 ), $line( ${-} ) );
	dim [ a 1 ];
	call assert( =( $len( a ) 1 ), $line( ${-} ) );
	try{ call $len( 0 ); } catch e {}
	call assert( e, $line( ${-} ) );
}

function testtup() {
	var t;
	let t = $tup( $cat( "a" "b" ), +( 1 2 ) );
	call assert( =( t 3 ), $line( ${-} ) );
	call assert( =( $cmp( t, "ab" ) 0 ), $line( ${-} ) );
}

function testfile() {
	var f;
	let f = $load( "test.tt" );
	call assert( =( $len( f ) $flen( "test.tt" ) ), $line( ${-} ) );
	call assert( =( $cmp( $parse( f ) "rem" ) 0 ), $line( ${-} ) );
}

function testargs() {
	call assert( >( $argc 0 ), $line( ${-} ) );
	call assert( =( $cmp( $argv( 0 ) "test.tt" ) 0 ), $line( ${-} ) );
}

function testtime() {
	call assert( =( $len( $time ) 25 ), $line( ${-} ) );
}

function testchild() {
	var e;
	call assert( =( $cmp( $child( ${ ( a ) } ), "a" ) 0 ), $line( ${-} ) );
	call assert( =( $cmp( $child( ELE ), "*" ) 0 ), $line( ${-} ) );
	try { call $child( 0 ); } catch e {}
	call assert( e, $line( ${-} ) );
}

function testnext() {
	var e;
	call assert( =( $cmp( $next( ${ 0 a } ), "a" ) 0 ), $line( ${-} ) );
	try { call $next( 0 ); } catch e {}
	call assert( e, $line( ${-} ) );
}

function testline() {
	var x, y, e;
	let x = $line( ${-} );
	let y = $line( ${-} );
	call assert( =( -( y x ) 1 ), $line( ${-} ) );
	try { call $line( a ); } catch e {}
	call assert( e, $line( ${-} ) );
}

function testquote() {
	var s;
	let s = $quote( $str( "\"\\" $asc( 127 ) ) );
	call assert( =( $cmp( s, "\"\\\"\\\\\\177\"" ) 0 ), $line( ${-} ) );
}

function testunquote() {
	var s;
	let s = $unquote( "\"\\\\\\176\"" );
	call assert( =( $cmp( s, "\\~" ) 0 ), $line( ${-} ) );
}

program test {
	call hello();
	call assert( =( testvoid() 0 ), $line( ${-} ) );
	call testvars( ONE );
	call testarray();
	call testfact();
	call testswitch();
	call testif();
	call testwhile();
	call testtry();
	call testinfix();
	call testarith();
	call testlogic();
	call testcmp();
	call testcat();
	call testchr();
	call testsub();
	call testasc();
	call testhex();
	call testint();
	call testlen();
	call testtup();
	call testfile();
	call testargs();
	call testtime();
	call testchild();
	call testnext();
	call testline();
	call testquote();
	call testunquote();

	print "Tests passed.";
}
