
rem {
	Generic run-length coder.
}

# Return the encoded length and encode to output if not null.
function rle_encode( input, output ) {
	var in_idx, in_end, out_idx, code, prev, run, lit;
	let in_end = -( $len( input ) 1 );
	while <e( in_idx in_end ) {
		let code = $chr( input in_idx );
		if &&( &&( =( code prev ) <( in_idx in_end ) ) <( run 128 ) ) {
			inc run;
		} else {
			let prev = code;
			if output {
				if ||( ||( >e( lit 127 ) run ) >e( in_idx in_end ) ) {
					if lit {
						set [ output out_idx ] = lit;
						inc out_idx;
						while lit {
							set [ output out_idx ] = $chr( input -( -( in_idx lit ) run ) );
							inc out_idx;
							dec lit;
						}
					}
					if run {
						set [ output out_idx ] = -( 0 run );
						inc out_idx;
						let run = 0;
					}
					if >e( in_idx in_end ) {
						set [ output out_idx ] = 1;
						inc out_idx;
						set [ output out_idx ] = code;
						inc out_idx;
					}
				}
			} else {
				if ||( ||( >e( lit 127 ) run ) >e( in_idx in_end ) ) {
					if lit {
						let out_idx = +( +( out_idx lit ) 1 );
						let lit = 0;
					}
					if run {
						inc out_idx;
						let run = 0;
					}
					if >e( in_idx in_end ) {
						let out_idx = +( out_idx 2 );
					}
				}
			}
			inc lit;
		}
		inc in_idx;
	}
	return out_idx;
}

# Return the decoded length and decode to output if not null.
function rle_decode( input, output ) {
	var in_idx, out_idx, prev, code;
	while <( in_idx $len( input ) ) {
		let code = $chr( input in_idx );
		inc in_idx;
		if output {
			while <( code 0 ) {
				set [ output out_idx ] = prev;
				inc out_idx;
				inc code;
			}
			while >( code 0 ) {
				let prev = $chr( input in_idx );
				inc in_idx;
				set [ output out_idx ] = prev;
				inc out_idx;
				let code = -( code 1 );
			}
		} else {
			if <( code 0 ) {
				let out_idx = -( out_idx code );
			} else {
				let in_idx = +( in_idx code );
				let out_idx = +( out_idx code );
			}
		}
	}
	return out_idx;
}

program rle {
	var org, buf, enc, out;
	if =( $argc 3 ) {
		let org = $load( $argv( 1 ) );
		print $cat( "Original length: " $len( org ) );
		
		let buf = $array( rle_encode( org 0 ) );
		print $cat( "Compressed length: " rle_encode( org buf ) );
		let enc = $sub( buf 0 $len( buf ) );
		
		let buf = $array( rle_decode( enc 0 ) );
		print $cat( "Decompressed length: " rle_decode( enc buf ) );
		let out = $sub( buf 0 $len( buf ) );

		if $cmp( org out ) {
			throw "Decoded data differs from original!";
		}
		save enc $argv( 2 );
	} else {
		error "Run-length compression.";
		error "Usage rle.tt input output.rle";
	}
}
