
rem { Test }

const ONE = $len( $array( 1 ) );
const TWO = +( ONE 1 );
const STR = "*";
const CAT = $str( "*" "*" );
const ELE = ${ ( * ) };

global g, g1 = 1, g2 = +( g1 1 );
array a, a1 = $array( 1 ), [ a2 g2 ] [ a3 ${ 1, 2, 3 } ];

struct struc {
	a,b,c,d,e;
};

struct exten( struc ) {
	f,g;
};

include "hello.tt";

function assert( value, elem ) {
	if !( value ) {
		throw $str( "Assertion failed on line " $line( elem ) );
	}
}

function assert_equals( expected, value, elem ) {
	if $cmp( expected value ) {
		throw $str( "Assertion failed on line " $line( elem )
			". Expected '" expected "', actual '" value "'." );
	}
}

function assert_throws( expected, elem ) {
	var err;
	try {
		call :( $function( elem ) );
	} catch err {
		if <e( $len( expected ) $len( err ) ) {
			let err = $sub( err 0 $len( expected ) );
		}
	}
	call assert_equals( expected, err, elem );
}

function except( elem ) {
	throw $tup( elem, $line( elem ) );
}

function setg( x ) {
	let g = x;
	return g;
}

function testvoid() {
}

function testvars( x ) {
	var y = x, z = +( y 1 );
	call assert_equals( x, y, ${-} );
	call assert_equals( +( x 1 ), z, ${-} );
	call assert_equals( 0, g, ${-} );
	call assert_equals( 1, g1, ${-} );
	call assert_equals( 2, g2, ${-} );
	call setg( y );
	call assert_equals( x, g, ${-} );
	call assert_equals( 1, TRUE, ${-} );
	call assert_equals( 0, FALSE, ${-} );
	return g;
}

function testarray() {
	var b, c, err, [ arr *( TWO TWO ) ];
	call setg( 0 );	
	call assert_throws( "1", ${ () { dim [ except( ${ 1 } ) setg( 2 ) ]; } } );
	call assert_equals( 0, g, ${-} );
	call assert_throws( "1", ${ () { dim [ a except( ${ 1 } ) ]; } } );
	call assert_throws( "Not an array.", ${ () { dim[ 0 0 ]; } } );
	call assert_throws( "Negative array size.", ${ () { dim[ a -1 ]; } } );
	call assert_equals( 0, $len( a ), ${-} );
	call assert_equals( 1, $len( a1 ), ${-} );
	call assert_equals( 2, $len( a2 ), ${-} );
	call assert_equals( 3, $len( a3 ), ${-} );
	call assert_equals( 4, $len( arr ), ${-} );
	dim [ a 3 ];
	set [ a,0 ] = "a";
	set [ a 1 ] = 1;
	set [ a 2 ] = [ a 0 ];
	dim [ a 2 ];
	call assert_throws( "Array index", ${ () { set [ a 2 ] = except( ${ 0 } ); } } );
	dim [ a 1 ];
	call assert_equals( "a", [ a 0 ], ${-} );
	let a = $array( ${ 0, $str( "\"one" "\"" ), $tup( "\\two\\"2 ), "\063", ${ four(4) }, 84, 84 } );
	let a = $array( $parse( $unparse( $values( a ) ) ) );
	call assert_equals( 7, $len( a ), ${-} );
	call assert_equals( 0, [ a 0 ], ${-} );
	call assert_equals( "\"one\"", [ a 1 ], ${-} );
	call assert_equals( "\\two\\", $str( [ a 2 ] ), ${-} );
	call assert_equals( 2, $tup( 0, [ a 2 ] ), ${-} );
	call assert_equals( "3", [ a 3 ], ${-} );
	call assert_equals( "four", [ a 4 ], ${-} );
	call assert_equals( "4", $child( $next( [ a 4 ] ) ), ${-} );
	call assert_equals( "TT", $sub( a, 5, 2 ), ${-} );
	call assert_equals( "#Array#", a, ${-} );
	call assert_equals( 1, $len( $array( 1 ) ), ${-} );
	call assert_throws( "Invalid array length.", ${ () { call $array( -1 ); } });
	let b = $array( 2 );
	let c = $array( ${ "a" 1 } );
	call assert_equals( "a", [ c 0 ], ${-} );
	call assert_equals( 1, [ c 1 ], ${-} );
	set [ b 0 ] = b;
	set [ b 1 ] = c;
	set [ c 0 ] = c;
	set [ c 1 ] = b;
}

function fact( x ) {
	return ?( <e( x 1 ) 1 *( x fact( -( x 1 ) ) ) );
}

function testswitch() {
	var x;
	switch $tup( "a" 1 ) {
		case 1   { call assert( FALSE, ${-} ); }
		case "a" { call assert( FALSE, ${-} ); }
		case $tup( "a" 1 ) { let x = 1; }
		default { call assert( FALSE, ${-} ); }
	}
	call assert_equals( 1, x, ${-} );
	switch "a" {
		case "a" { let x = 2; }
		default { call assert( FALSE, ${-} ); }
	}
	call assert_equals( 2, x, ${-} );
	switch 1 {
		case 1 { let x = 3; }
		default { call assert( FALSE, ${-} ); }
	}
	call assert_equals( 3, x, ${-} );
	switch 1 {
		default { let x = 4; }
	}
	call assert_equals( 4, x, ${-} );
	switch 3 {
		case 0, setg( 0 ) setg( 4 ) { let x = 5; }
	}
	call assert_equals( 4, x, ${-} );
	call assert_equals( 4, g, ${-} );
	call assert_throws( "1", ${ () { switch except( ${ 1 } ) {} } } );
	call assert_throws( "1", ${ () { switch 0 { case except( ${ 1 } ) { call except( ${ 2 } ); } } } } );
	call assert_throws( "1", ${ () { switch 0 { case 0 { call except( ${ 1 } ); } } } } );
}

function testif() {
	var x;
	if FALSE { call assert( FALSE, ${-} ); }
	if TRUE { let x = 1; } else { call assert( FALSE, ${-} ); }
	call assert_equals( 1, x, ${-} );
	if "" { let x = 2; }
	call assert_equals( 2, x, ${-} );
	call assert_throws( "1", ${ () { if except( ${ 1 } ) { call except( ${ 2 } ); } } } );
}

function testwhile() {
	var n;
	while <( n 1 ) { inc n; }
	call assert_equals( 1, n, ${-} );
	while <( n 10 ) {
		break;
		call assert( FALSE, ${-} );
	}
	while <( n 10 ) {
		if =( n 5 ) { break; }
		inc n;
		continue;
		call assert( FALSE, ${-} );
	}
	call assert_equals( 5, n, ${-} );
	while TRUE {
		try { break; } catch n { call assert( FALSE, ${-} ); }
		call assert( FALSE, ${-} );
	}
	while TRUE {
		try { throw 0; } catch n { break; }
		call assert( FALSE, ${-} );
	}
	call assert_equals( 1, :( $function( ${ () { while TRUE { return 1; } return 0; } } ) ), ${-} );
	var off, end = 10;
	while <( off end ) {
		var aaa, bbb = 0;
		inc aaa;
		inc bbb;
		inc off;
	}
	call assert_equals( 10, off, ${-} );
	call assert_equals( 10, aaa, ${-} );
	call assert_equals( 1, bbb, ${-} );
	call assert_throws( "1", ${ () { while except( ${ 1 } ) { call assert( FALSE, ${-} ); } } } );
	call assert_throws( "1", ${ () { while TRUE { call except( ${ 1 } ); } } } );
}

function testtry() {
	var e;
	try {} catch e { call assert( FALSE, ${-} ); }
	try {
		try {
			throw 1;
		} catch e {
			call assert_equals( 1, e, ${-} );
			throw 2;
		}
	} catch e {
	}
	call assert_equals( 2, e, ${-} );
	try { try { return 0; } catch e {} } catch e {}
	call assert( FALSE, ${-} );
}

function testinfix() {
	call assert( '( ONE = 1 ), ${-} );
}

function testarith() {
	call assert_equals( 4, +( +( 1 1 ) +( 1 1 ) ), ${-} );
	call assert_equals( 5, +( 1 1 2 1 ), ${-} );
	call assert_equals( 3, +( ONE TWO ), ${-} );
	call assert_equals( 0, -( 1 1 ), ${-} );
	call assert_equals( 4, *( 2 2 ), ${-} );
	call assert_equals( 3, /( 6 2 ), ${-} );
	call assert_equals( 2, %( 6 4 ), ${-} );
	call assert_equals( 8, <<( 4 1 ), ${-} );
	call assert_equals( -4, >>( -8 1 ), ${-} );
	call assert_equals( 1, =( 1 1 ), ${-} );
	call assert_equals( 0, =( 1 0 ), ${-} );
	call assert_equals( 1, <( 0 1 ), ${-} );
	call assert_equals( 0, <( 1 1 ), ${-} );
	call assert_equals( 1, <e( 0 1 ), ${-} );
	call assert_equals( 1, <e( 1 1 ), ${-} );
	call assert_equals( 0, <e( 1 0 ), ${-} );
	call assert_equals( 1, >( 1 0 ), ${-} );
	call assert_equals( 0, >( 1 1 ), ${-} );
	call assert_equals( 1, >e( 1 0 ), ${-} );
	call assert_equals( 1, >e( 1 1 ), ${-} );
	call assert_equals( 0, >e( 0 1 ), ${-} );
	call assert_equals( 0, &( 1 2 ), ${-} );
	call assert_equals( 2, &( 2 2 ), ${-} );
	call assert_equals( 3, |( 1 2 ), ${-} );
	call assert_equals( 1, ^( 1 0 ), ${-} );
	call assert_equals( 0, ^( 1 1 ), ${-} );
	call assert_equals( -1, ~( 0 ), ${-} );
	call assert_throws( "1", ${ () { call +( except( ${ 1 } ) except( ${ 0 } ) ); } } );
	call assert_throws( "1", ${ () { call +( 0 except( ${ 1 } ) ); } } );
	call assert_throws( "Integer division by zero", ${ () { call /( 1 0 ); } } );
	call assert_throws( "Modulo division by zero", ${ () { call %( 1 0 ); } } );
}

function testlogic() {
	call assert_equals( 1, !( 0 ), ${-} );
	call assert_equals( 0, !( 1 ), ${-} );
	call assert_equals( 0, !( "" ), ${-} );
	call assert_equals( 0, &&( 0 0 ), ${-} );
	call assert_equals( 0, &&( 0 1 ), ${-} );
	call assert_equals( 0, &&( 1 0 ), ${-} );
	call assert_equals( 1, &&( 1 1 ), ${-} );
	call assert_equals( 1, &&( 1 1 1 ), ${-} );
	call assert_equals( 1, &&( 1 "a" ), ${-} );
	call assert_equals( 1, &&( "a" 1 ), ${-} );
	call assert_equals( 0, &&( 0 except( ${-} ) ), ${-} );
	call assert_equals( 0, ||( 0 0 ), ${-} );
	call assert_equals( 1, ||( 0 1 ), ${-} );
	call assert_equals( 1, ||( 1 0 ), ${-} );
	call assert_equals( 1, ||( 1 1 ), ${-} );
	call assert_equals( 1, ||( 0 0 1 ), ${-} );
	call assert_equals( 1, ||( 0 "a" ), ${-} );
	call assert_equals( 1, ||( "a" 0 ), ${-} );
	call assert_equals( 1, ||( 1 except( ${-} ) ), ${-} );
	call assert_throws( "1", ${ () { call !( except( ${ 1 } ) ); } } );
	call assert_throws( "1", ${ () { call &&( except( ${ 1 } ) 1 ); } } );
	call assert_throws( "1", ${ () { call &&( 1 except( ${ 1 } ) ); } } );
	call assert_throws( "1", ${ () { call ||( except( ${ 1 } ) 0 ); } } );
	call assert_throws( "1", ${ () { call ||( 0 except( ${ 1 } ) ); } } );
}

function testternary() {
	call assert_equals( 1, ?( 0, except( ${-} ), 1 ), ${-} );
	call assert_equals( 1, ?( 1, 1, except( ${-} ) ), ${-} );
	call assert_throws( "1", ${ () { call ?( except( ${ 1 } ), 0, 0 ); } } );
	call assert_throws( "1", ${ () { call ?( 0, 0, except( ${ 1 } ) ); } } );
	call assert_throws( "1", ${ () { call ?( 1, except( ${ 1 } ), 0 ); } } );
}

function testcmp() {
	call assert_equals( 0, $cmp( 0 0 ), ${-} );
	call assert_equals( 1, $cmp( 1 0 ), ${-} );
	call assert_equals( -1, $cmp( 0 1 ), ${-} );
	call assert_equals( 1, $cmp( "a" 0 ), ${-} );
	call assert_equals( -1, $cmp( 0 "a" ), ${-} );
	call assert_equals( 0, $cmp( "" "" ), ${-} );
	call assert_equals( 0, $cmp( "0" "0" ), ${-} );
	call assert_equals( 0, $cmp( STR "*" ), ${-} );
	call assert_equals( 0, $cmp( CAT "**" ), ${-} );
	call assert_equals( 0, $cmp( $tup( "a" 1 ) $tup( "a" 1 ) ), ${-} );
	call assert_equals( 1, $cmp( 1 0 ), ${-} );
	call assert_equals( 1, $cmp( "1" "0" ), ${-} );
	call assert_equals( -1, $cmp( "0" "1" ), ${-} );
	call assert_equals( 1, $cmp( "aa" "a" ), ${-} );
	call assert_equals( -1 $cmp( "a" "aa" ), ${-} );
	call assert_equals( -1, $cmp( "b" "ba" ), ${-} );
	call assert_equals( -1, $cmp( "ba" "bb" ), ${-} );
	call assert_equals( -1, $cmp( $tup( "a" 0 ) $tup( "b" 1 ) ), ${-} );
	call assert_equals( -1, $cmp( $tup( "a" 1 ) $tup( "b" 1 ) ), ${-} );
	call assert_throws( "1", ${ () { call $cmp( except( ${ 1 } ) "" ); } } );
	call assert_throws( "1", ${ () { call $cmp( "" except( ${ 1 } ) ); } } );
}

function testcat() {
	call assert_equals( 0, $cmp( $cat( 0 ) "0" ), ${-} );
	call assert_equals( 0, $cmp( $cat( 0 1 ) "01" ), ${-} );
	call assert_equals( 0, $cmp( $cat( "" 0 ) "0" ), ${-} );
	call assert_equals( 0, $cmp( $cat( 0 "" ) "0" ), ${-} );
	call assert_equals( 0, $cmp( $cat( 0 "1" "2" ) "012" ), ${-} );
	call assert_equals( 0, $cmp( $cat( a ) a ), ${-} );
	call assert_throws( "1", ${ () { call $cat( except( ${ 1 } ) "" ); } } );
	call assert_throws( "1", ${ () { call $cat( "" except( ${ 1 } ) ); } } );
}

function testchr() {
	call assert_equals( 126, $chr( "~" 0 ), ${-} );
	call assert_equals( -1, $chr( "\377" 0 ), ${-} );
	call assert_throws( "1", ${ () { call $chr( except( ${ 1 } ) 0 ); } } );
	call assert_throws( "1", ${ () { call $chr( "" except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string", ${ () { call $chr( 0 0 ); } } );
	call assert_throws( "String index", ${ () { call $chr( "" 0 ); } } );
}

function testsub() {
	call assert_equals( 0, $cmp( $sub( "abc" 1 1 ) "b" ), ${-} );
	call assert_equals( 0, $cmp( $sub( $array( ${ 65, 66, 67 } ) 0 3 ) "ABC" ), ${-} );
	call assert_throws( "1", ${ () { call $sub( except( ${ 1 } ) 0 0 ); } } );
	call assert_throws( "1", ${ () { call $sub( "" except( ${ 1 } ) 0 ); } } );
	call assert_throws( "1", ${ () { call $sub( "" 0 except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string", ${ () { call $sub( 0 0 0 ); } } );
	call assert_throws( "Range out of bounds", ${ () { call $sub( $array( 1 ) 1 1 ); } } );
	call assert_throws( "Range out of bounds", ${ () { call $sub( "a" 1 1 ); } } );
	call assert_throws( "Range out of bounds", ${ () { call $sub( "a" -1 1 ); } } );
	call assert_throws( "Range out of bounds", ${ () { call $sub( "a" 0 -1 ); } } );
}

function testasc() {
	call assert_equals( 0, $cmp( $asc( 126 ) "~" ), ${-} );
	call assert_throws( "1", ${ () { call $asc( except( ${ 1 } ) ); } } );
}

function testhex() {
	call assert_equals( 0, $cmp( $hex(  0x00000000 ) " 0x00000000" ), ${-} );
	call assert_equals( 0, $cmp( $hex( -0x00000001 ) "-0x00000001" ), ${-} );
	call assert_equals( 0, $cmp( $hex( -0x80000000 ) "-0x80000000" ), ${-} );
	call assert_throws( "1", ${ () { call $hex( except( ${ 1 } ) ); } } );
}

function testint() {
	call assert_equals( 1, $int( "1" ), ${-} );
	call assert_equals( 10, $int( "0xA" ), ${-} );
	call assert_equals( 8, $int( "010" ), ${-} );
	call assert_throws( "1", ${ () { call $int( except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string", ${ () { call $int( 1 ); } } );
	call assert_throws( "Unable to convert", ${ () { call $int( "a" ); } } );
}

function testlen() {
	call assert_equals( 0, $len( "" ), ${-} );
	call assert_equals( 1, $len( "a" ), ${-} );
	call assert_equals( 1, $len( $array( 1 ) ), ${-} );
	call assert_throws( "1", ${ () { call $len( except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string or array", ${ () { call $len( 0 ); } } );
}

function testtup() {
	call assert_equals( 1,  +( $tup( "a", 1 ) 0 ), ${-} );
	call assert_equals( 1,  $tup( 0, $tup( "a", 1 ) ), ${-} );
	call assert_equals( "a", $tup( $tup( "a", 1 ), $tup( "b" 0 ) ), ${-} );
	call assert_throws( "1", ${ () { call $tup( except( ${ 1 } ) 0 ); } } );
	call assert_throws( "1", ${ () { call $tup( "", except( ${ 1 } ) ); } } );
}

function testload() {
	call assert( $chr( $load( $src ) 0 ), ${-} );
	call assert_throws( "1", ${ () { call $load( except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string", ${ () { call $load( 0 ); } } );
}

function testflen() {
	call assert_equals( $flen( $src ), $len( $load( $src ) ), ${-} );
	call assert_throws( "1", ${ () { call $flen( except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string", ${ () { call $flen( 0 ); } } );
}

function testargc() {
	call assert( >( $argc 0 ), ${-} );
}

function testargv() {
	call assert_equals( $src, $argv( 0 ), ${-} );
	call assert_throws( "1", ${ () { call $argv( except( ${ 1 } ) ); } } );
	call assert_throws( "Command-line argument index", ${ () { call $argv( -1 ); } } );
}

function testtime() {
	call assert_equals( 25, $len( $time ), ${-} );
}

function testparse() {
	var elem = $parse( "rem { Test }" );
	call assert_equals( "rem", elem, ${-} );
	call assert_equals( "Test", $child( $next( elem ) ), ${-} );
	call assert_throws( "1", ${ () { call $parse( except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string", ${ () { call $parse( 0 ); } } );
	call assert_throws( "Unclosed string", ${ () { call $parse( "\""); } } );
}

function testunparse() {
	var elem = $parse( $unparse( ${ rem { Test } } ) );
	call assert_equals( "rem", elem, ${-} );
	call assert_equals( "Test", $child( $next( elem ) ), ${-} );
	call assert_throws( "1", ${ () { call $unparse( except( ${ 1 } ) ); } } );
	call assert_throws( "Not an element", ${ () { call $unparse( 0 ); } } );
}

function testchild() {
	call assert_equals( "a", $child( ${ ( a ) } ), ${-} );
	call assert_equals( "*", $child( ELE ), ${-} );
	call assert_throws( "1", ${ () { call $child( except( ${ 1 } ) ); } } );
	call assert_throws( "Not an element", ${ () { call $child( 0 ); } } );
}

function testnext() {
	call assert_equals( "a", $next( ${ 0 a } ), ${-} );
	call assert_throws( "1", ${ () { call $next( except( ${ 1 } ) ); } } );
	call assert_throws( "Not an element", ${ () { call $next( 0 ); } } );
}

function testline() {
	var x = $line( ${-} );
	var y = $line( ${-} );
	call assert_equals( 1, -( y x ), ${-} );
	call assert_throws( "1", ${ () { call $line( except( ${ 1 } ) ); } } );
	call assert_throws( "Not an element", ${ () { call $line( 0 ); } } );
}

function testquote() {
	call assert_equals( "\"\\\"\\\\\\177\"", $quote( $str( "\"\\" $asc( 127 ) ) ), ${-} );
	call assert_throws( "1", ${ () { call $quote( except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string", ${ () { call $quote( 0 ); } } );
}

function testunquote() {
	call assert_equals( "\\~", $unquote( "\"\\\\\\176\"" ), ${-} );
	call assert_throws( "1", ${ () { call $unquote( except( ${ 1 } ) ); } } );
	call assert_throws( "Not a string", ${ () { call $unquote( 0 ); } } );
}

function testpack() {
	call assert_equals( "\0\0\0\0", $pack( 0 ), ${-} );
	call assert_equals( "\0\0\0\1", $pack( 1 ), ${-} );
	call assert_equals( "\1\2\3\4", $pack( 0x01020304 ), ${-} );
	call assert_equals( "\0\0\0\0\1\2\3\4", $pack( $array( ${ 0, 0x1020304 } ) ), ${-} );
	call assert_throws( "1", ${ () { call $pack( except( ${ 1 } ) ); } } );
}

function testunpack() {
	call assert_equals( 1, $unpack( "\0\0\0\1" 0 ), ${-} );
	call assert_equals( 0x1020304, $unpack( "\1\2\3\4" 0 ), ${-} );
	call assert_equals( -1, $unpack( $pack( -1 ) 0 ), ${-} );
	call assert_equals( 2, $unpack( $pack( $array( ${ 1 2 } ) ) 1 ), ${-} );
	call assert_throws( "1", ${ () { call $unpack( except( ${ 1 } ) 0 ); } } );
	call assert_throws( "1", ${ () { call $unpack( "" except( ${ 1 } ) ); } } );
	call assert_throws( "String index", ${ () { call $unpack( "\0\0\0\0" -1 ); } } );
	call assert_throws( "String index", ${ () { call $unpack( "\0\0\0" 0 ); } } );
}

function teststruct() {
	call assert_equals( 5, struc, ${-} );
	call assert_equals( 7, exten, ${-} );
	call assert_equals( 0, struc.a, ${-} );
	call assert_equals( 4, struc.e, ${-} );
	call assert_equals( 6, exten.g, ${-} );
	call assert_equals( 7, $len( $new( exten ) ), ${-} );
}

function testfuncref() {
	call assert_equals( "testfuncref", @testfuncref, ${-} );
	call setg( 0 );
	call :( @setg 1 );
	call assert_equals( 1, g, ${-} );
	call assert_throws( "Not a function reference.", ${ () { call :( $tup( "a" 1 ) ); } } );
	call assert_throws( "Incorrect number of parameters", ${ () { call :( @setg 1 2 ); } } );
}

function testeq() {
	call assert_equals( 1, $eq( 0 0 ), ${-} );
	call assert_equals( 1, $eq( "" "" ), ${-} );
	call assert_equals( 1, $eq( "a" "a" ), ${-} );
	call assert_equals( 1, $eq( STR STR ), ${-} );
	call assert_equals( 1, $eq( @testeq @testeq ), ${-} );
	call assert_equals( 0, $eq( 0 1 ), ${-} );
	call assert_equals( 0, $eq( 0 "a" ), ${-} );
	call assert_equals( 0, $eq( "a" 0 ), ${-} );
	call assert_equals( 0, $eq( "a" "aa" ), ${-} );
	call assert_throws( "1", ${ () { call setg( 0 ); call $eq( except( ${ 1 } ) setg( 2 ) ); } } );
	call assert_equals( 0, g, ${-} );
	call assert_throws( "2", ${ () { call $eq( 0, except( ${ 2 } ) ); } } );
}

function testsrc() {
	call assert_equals( "test.tt", $sub( $src, -( $len( $src ) 7 ), 7 ), ${-} );
}

function testchop() {
	call assert_equals( "", $chop( "", "" ), ${-} );
	call assert_equals( "", $chop( "file", "" ), ${-} );
	call assert_equals( "", $chop( "file", "/" ), ${-} );
	call assert_equals( "dir/", $chop( "dir/file", "/" ), ${-} );
	call assert_equals( "c:", $chop( "c:file", "/:" ), ${-} );
	call assert_equals( "dir\\", $chop( "dir\\file", "/:\\" ), ${-} );
	call assert_equals( "c:\\dir\\", $chop( "c:\\dir\\file", "/:\\" ), ${-} );
	call assert_throws( "1", ${ () { call setg( 0 ); call $chop( except( ${ 1 } ), setg( 2 ) ); } } );
	call assert_equals( 0, g, ${-} );
	call assert_throws( "2", ${ () { call $chop( setg( 1 ), except( ${ 2 } ) ); } } );
	call assert_equals( 1, g, ${-} );
	call assert_throws( "Not a string.", ${ () { call $chop( 0, "" ); } } );
	call assert_throws( "Not a string.", ${ () { call $chop( "", 0 ); } } );
}

function testinterrupted() {
	var x;
	let x = STR;
	let x = $interrupted;
	call assert( !( x ), ${-} );
}

function testinc() {
	var int;
	inc int;
	call assert_equals( 1, int, ${-} );
	call assert_throws( "Not an integer.", ${ () { var int = ""; inc int; } } );
}

function testdec() {
	var int;
	dec int;
	call assert_equals( -1, int, ${-} );
	call assert_throws( "Not an integer.", ${ () { var int = ""; dec int; } } );
}

function testelem() {
	call assert_equals( "1", $elem( ${ 1 }, 0, 0 ), ${-} );
	call assert_equals( "2", $child( $elem( ${()}, ${ 2 }, 0 ) ), ${-} );
	call assert_equals( "2", $child( $elem( ${[]}, ${ 2 }, 0 ) ), ${-} );
	call assert_equals( "2", $child( $elem( ${{}}, ${ 2 }, 0 ) ), ${-} );
	call assert_equals( "2", $child( $elem( ${{}}, $elem( ${ 2 }, 0, 0 ), 0 ) ), ${-} );
	call assert_equals( "3", $next( $elem( ${ 1 }, 0, ${ 3 } ) ), ${-} );
	call assert_equals( "3", $next( $elem( ${ 1 }, 0, $elem( ${ 3 }, 0, 0 ) ) ), ${-} );
	call assert_equals( "()", $elem( $parse( "(abc) def" ), 0, 0 ), ${-} );
	call assert_equals( 0, $child( $elem( $parse( "(abc) def" ), 0, 0 ) ), ${-} );
	call assert_equals( "2", $child( $elem( $parse( "(abc) def" ), ${ 2 }, 0 ) ), ${-} );
	call assert_equals( 0, $next( $elem( $parse( "(abc) def" ), 0, 0 ) ), ${-} );
	call assert_equals( "3", $next( $elem( $parse( "(abc) def" ), ${ 2 }, ${ 3 } ) ), ${-} );
	call assert_equals( "3", $next( $elem( $elem( ${()}, ${ abc }, ${ def } ), ${ 2 }, ${ 3 } ) ), ${-} );
	call assert_throws( "1", ${ () { call $elem( except( ${ 1 } ), 0, 0 ); } } );
	call assert_throws( "Not an element.", ${ () { call $elem( 1, 0, 0 ); } } );
	call assert_throws( "2", ${ () { call $elem( ${-}, except( ${ 2 } ), 0 ); } } );
	call assert_throws( "Not an element.", ${ () { call $elem( ${-}, 2, 0 ); } } );
	call assert_throws( "3", ${ () { call $elem( ${-}, 0, except( ${ 3 } ) ); } } );
	call assert_throws( "Not an element.", ${ () { call $elem( ${-}, 0, 3 ); } } );
	call assert_throws( "Parent elements must", ${ () { call $elem( ${-}, ${ 2 }, 0 ); } } );
}

function testvalues() {
	var arr = $array( $parse( $unparse( $values( $array( ${ 0 1 "2" $tup( "Three" 3 ) ${ [4] } } ) ) ) ) );
	call assert_equals( 0, [ arr 0 ], ${-} );
	call assert_equals( 1, [ arr 1 ], ${-} );
	call assert_equals( "2", [ arr 2 ], ${-} );
	call assert_equals( $tup( "Three" 3 ), [ arr 3 ], ${-} );
	call assert_equals( "4", $child( [ arr 4 ] ), ${-} );
	call assert_throws( "1", ${ () { call $values( except( ${ 1 } ) ); } } );
	call assert_throws( "Not an array or no values.", ${ () { call $values( 0 ); } } );
	call assert_throws( "Not an array or no values.", ${ () { call $values( $array( 0 ) ); } } );
}

function testfunction() {
	call assert_equals( ONE :(
		$function( ${ () { 
			return :( $function( $parse( "( param ) { return param; }" ) ) ONE );
		} } ) ), ${-} );
	call assert_throws( "1", ${ () { call $function( except( ${ 1 } ) ); } } );
	call assert_throws( "Not an element.", ${ () { call $function( 0 ); } } );
	call assert_throws( "Expected '(' after '$function'", ${ () { call $function( ${ x } ); } } );
	call assert_throws( "Invalid name '1'", ${ () { call $function( ${ ( 1 ) {} } ); } } );
	call assert_throws( "Parameter 'x' already", ${ () { call $function( ${ ( x x ) {} } ); } } );
	call assert_throws( "Unrecognized keyword 'x'", ${ () { call $function( ${ () { x } } ); } } );
}

function testworker() {
	var worker = $execute( $worker( ${ () { call $status( "1" ); return 1; } } ) );
	call assert_equals( 1, $result( worker ), ${-} );
	call assert_equals( "1", $status( worker ), ${-} );
	call assert_equals( "1", $result( $execute( $worker( ${ ( a ) { return a; } } ), "1" ) ), ${-} );
	call assert_throws( "1", ${ () { call $result( $execute( $worker( ${ ( a ) { throw a; } } ), "1" ) ); } } );
	call assert_throws( "Not an element.", ${ () { call $worker( 0 ); } } );
	call assert_throws( "Expected '(' after '$worker'", ${ () { call $worker( ${ x } ); } } );
	call assert_throws( "Invalid name '1'", ${ () { call $worker( ${ ( 1 ) {} } ); } } );
	call assert_throws( "Parameter 'x' already", ${ () { call $worker( ${ ( x x ) {} } ); } } );
	call assert_throws( "Not a worker", ${ () { call $execute( 0 ); } } );
	call assert_throws( "Incorrect number", ${ () { call $execute( $worker( ${ ( a ) {} } ) ); } } );
	call assert_throws( "Values of this type", ${ () { call $execute( $worker( ${ ( a ) {} } ), ${ x } ); } } );
	call assert_throws( "Not a worker", ${ () { call $result( 0 ); } } );
	call assert_throws( "Worker exited", ${ () { call $result( $execute( $worker( ${ () { exit 0; } } ) ) ); } } );
	call assert_throws( "Values of this type", ${ () { call $result( $execute( $worker( ${ () { return $array( 0 ); } } ) ) ); } } );
	call assert_throws( "Operation", ${ () { call $result( $execute( $worker( ${ () { call $worker( 0 ); } } ) ) ); } } );
	call assert_throws( "Not a worker", ${ () { call $status( 0 ); } } );
	call assert_throws( "Worker exited", ${ () { var err, worker = $execute( $worker( ${ () { exit 0; } } ) );
		try { call $result( worker ); } catch err { call $status( worker ); } } } );
	call assert_throws( "x", ${ () { var err, worker = $execute( $worker( ${ () { throw "x"; } } ) );
		try { call $result( worker ); } catch err { call $status( worker ); } } } );
}

program test {
	call hello();
	call assert( =( testvoid() 0 ), ${-} );
	call assert( =( testvars( ONE ) ONE ), ${-} );
	call testarray();
	call assert( =( fact( 3 ) 6 ), ${-} );
	call testswitch();
	call testif();
	call testwhile();
	call testtry();
	call testinfix();
	call testarith();
	call testlogic();
	call testternary();
	call testcmp();
	call testcat();
	call testchr();
	call testsub();
	call testasc();
	call testhex();
	call testint();
	call testlen();
	call testtup();
	call testsrc();
	call testload();
	call testflen();
	call testargc();
	call testargv();
	call testtime();
	call testparse();
	call testunparse();
	call testchild();
	call testnext();
	call testline();
	call testelem();
	call testquote();
	call testunquote();
	call testpack();
	call testunpack();
	call teststruct();
	call testfuncref();
	call testeq();
	call testchop();
	call testinterrupted();
	call testinc();
	call testdec();
	call testvalues();
	call testfunction();
	call testworker();
	print "Tests passed.";
}
