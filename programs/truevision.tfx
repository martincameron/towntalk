
# Return the width of the specified TGA image file.
function truevision_get_width( tga ) {
	return +( *( $chr( tga 13 ) 256 ) &( $chr( tga 12 ) 0xFF ) );
}

# Return the height of the specified TGA image file.
function truevision_get_height( tga ) {
	return +( *( $chr( tga 15 ) 256 ) &( $chr( tga 14 ) 0xFF ) );
}

# Return an array containing the palette contained within the specified TGA image file.
function truevision_get_palette( tga ) {
	var palbits, offset, palette, idx, end, clr, alpha;
	if !( &&( =( $chr( tga 1 ) 1 ) =( $chr( tga 16 ) 8 ) ) ) {
		throw "Palette data not present in file or unsupported.";
	}
	let palbits = $chr( tga 7 );
	if !( ||( =( palbits 24 ) =( palbits 32 ) ) ) {
		throw $cat( "Unsupported palette entry size (" palbits " bits)." );
	}
	# Skip Image ID area.
	let offset = +( $chr( tga 0 ) 18 );
	let palette = $array( 256 );
	let idx = $chr( tga 3 );
	let end = +( idx +( *( $chr( tga 6 ) 256 ) &( $chr( tga 5 ) 0xFF ) ) );
	while <( idx end ) {
		# Read palette entry.
		let clr = $chr( tga +( offset 2 ) );
		let clr = +( *( clr 256 ) $chr( tga +( offset 1 ) ) );
		let clr = +( *( clr 256 ) $chr( tga offset ) );
		let offset = +( offset 3 );
		if =( palbits 32 ) {
			let alpha = $chr( tga offset );
			inc offset;
		} else {
			let alpha = 0xFF;
		}
		set [ palette idx ] = +( *( clr 256 ) alpha );
		inc idx;
	}
	return palette;
}

# Return an array containing the image-data from the specified TGA image file.
function truevision_get_image_data( tga ) {
	var imgtype, length, offset, data, idx, run, end, clr;
	let imgtype = $chr( tga 2 );
	if !( ||( =( imgtype 1 ) =( imgtype 9 ) ) ) {
		throw $cat( "Unsupported image type (" imgtype "). Expected colour-mapped image." );
	}
	if !( =( $chr( tga 16 ) 8 ) ) {
		throw "Unsupported image data. Expected 8 bits per pixel.";
	}
	# Skip image ID and palette area.
	let length = +( *( $chr( tga 6 ) 256 ) &( $chr( tga 5 ) 0xFF ) );
	let offset = +( +( $chr( tga 0 ) 18 ) *( length >>( $chr( tga 7 ) 3 ) ) );
	# Read image data.
	let data = $array( *( truevision_get_width( tga ) truevision_get_height( tga ) ) );
	let idx = 0;
	let length = $len( data );
	if =( imgtype 1 ) {
		# Uncompressed.
		while <( idx length ) {
			# Lookup palette index.
			set [ data idx ] = &( $chr( tga offset ) 0xFF );
			inc offset;
			inc idx;
		}
	} else {
		# Run-length coding.
		while <( idx length ) {
			let run = &( $chr( tga offset ) 0xFF );
			inc offset;
			if >( run 127 ) {
				# Run-length packet.
				let end = -( +( idx run ) 127 );
				let clr = &( $chr( tga offset ) 0xFF );
				inc offset;
				while <( idx end ) {
					set [ data idx ] = clr;
					inc idx;
				}
			} else {
				# Literal packet.
				let end = +( +( idx run ) 1 );
				while <( idx end ) {
					set [ data idx ] = &( $chr( tga offset ) 0xFF );
					inc offset;
					inc idx;
				}
			}
		}
	}
	return data;
}

# Return the RGBA pixel data from the specified TGA image file.
function truevision_get_pixels( tga ) {
	var palette, pixels, idx, len;
	# Read palette.
	let palette = truevision_get_palette( tga );
	# Read image data.
	let pixels = truevision_get_image_data( tga );
	let idx = 0;
	let len = $len( pixels );
	while <( idx len ) {
		# Look-up palette index.
		set [ pixels idx ] = [ palette [ pixels idx ] ];
		inc idx;
	}
	return pixels;
}

program truevision {
	var tga, width, height, pixels;
	let tga = $load( $argv( 1 ) );
	
	let width = truevision_get_width( tga );
	let height = truevision_get_height( tga );
	let pixels = truevision_get_pixels( tga );
	
	fxopen width height "Truevision";
	fxsurface 0, width, height, pixels;
	fxblit 0, 0, 0, width, height, 0, 0;
	
	while TRUE {
		fxshow;
		call $fxwait;
	}
}
