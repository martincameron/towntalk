
struct object {
	to_string
}

struct comparable( object ) {
	compare
}

struct number( comparable ) {
	value;
}

function compare_numbers( ( number ) lhs, ( number ) rhs ) {
	return -( lhs.value rhs.value );
}

function number_to_string( ( number ) this ) {
	return $str( this.value );
}

function new_number( value ) {
	return $new( number @number_to_string @compare_numbers value );
}

# Sort an array of comparable instances.
function qsort( array offset end ) {
	if <( -( end offset ) 2 ) {
		return array;
	}
	var idx = offset, pivot_idx = >>( +( offset end ) 1 );
	var ( comparable ) pivot = [ array pivot_idx ];
	let [ array pivot_idx ] = [ array idx ];
	let [ array idx ] = pivot;
	let pivot_idx = idx++;
	while <( idx end ) {
		if >( :pivot.compare( [ array idx ] ) 0 ) {
			let [ array pivot_idx++ ] = [ array idx ];
			let [ array idx ] = [ array pivot_idx ];
			let [ array pivot_idx ] = pivot;
		}
		inc idx;
	}
	# Select the smallest region for recursion.
	if <( -( pivot_idx offset ) -( end pivot_idx ) ) {
		call qsort( array offset pivot_idx );
		let offset = +( pivot_idx 1 );
		# Skip values equal to pivot.
		while &&( <( offset end ) =( :pivot.compare( [ array offset ] ) 0 ) ) {
			inc offset;
		}
	} else {
		call qsort( array +( pivot_idx 1 ) end );
		let end = pivot_idx;
	}
	# Tail-call.
	return qsort( array offset end );
}

function sorted( comparables ) {
	var idx, len = $len( comparables );
	var ( comparable ) prev = [ comparables idx++ ];
	while <( idx len ) {
		var ( comparable ) curr = [ comparables idx++ ];
		if >( :prev.compare( curr ) 0 ) {
			return FALSE;
		}
		let prev = curr;
	}
	return TRUE;
}

function numbers( array ) {
	var idx, len = $len( array ), out = $array( len );
	while <( idx len ) {
		let [ out idx ] = new_number( [ array idx++ ] );
	}
	return out;
}

function random( len mul incr ) {
	var array = $array( len ), idx, seed;
	while <( idx len ) {
		let seed = &( +( *( seed mul ) incr ) 0x1FFFFFFF );
		let [ array idx++ ] = seed;
	}
	return array;
}

function range( len start step ) {
	var array = $array( len ), idx;
	while <( idx len ) {
		let [ array idx++ ] = start;
		let start = +( start step );
	}
	return array;
}

program test {
	var len = 65536;
	print sorted( qsort( numbers( random( len 65 17 ) ) 0, len ) );
	print sorted( qsort( numbers( range( len 0 1 ) ) 0, len ) );
	print sorted( qsort( numbers( range( len -( len 1 ) -1 ) ) 0, len ) );
	print sorted( qsort( numbers( range( len 1 0 ) ) 0, len ) );
}
